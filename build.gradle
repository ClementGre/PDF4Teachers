/*
 * Copyright (c) 2019-2022. Clément Grennerat
 * All rights reserved. You must refer to the licence Apache 2.
 */

plugins {
    id 'application'
    id 'org.beryx.jlink' version '2.24.4'
    id 'idea'
    id 'com.dua3.javafxgradle7plugin' version '0.0.9'
    id "de.jjohannes.extra-java-module-info" version "0.8"
}
repositories {
    mavenCentral()
    maven { url "https://clojars.org/repo" }
    flatDir {
        dirs 'locallibs'
    }
}

extraJavaModuleInfo {
    failOnMissingModuleInfo.set(false)

    automaticModule("jlatexmath-1.0.7.jar", "jlatexmath")
    automaticModule("writer2latex.jar", "writertolatex")

    automaticModule("jlatexmath-font-greek-1.0.7.jar", "jlatexmath.font.greek")
    automaticModule("jlatexmath-font-cyrillic-1.0.7.jar", "jlatexmath.font.cyrillic")

    automaticModule("xmpcore-6.0.6.jar", "xmpcore")

    automaticModule("metadata-extractor-2.16.0.jar", "metadata.extractor")

    automaticModule("commons-logging-1.2.jar", "commons.logging")

    automaticModule("jai-imageio-core-1.4.0.jar", "imageio.core")
    automaticModule("jai-imageio-jpeg2000-1.4.0.jar", "jai.imageio.jpeg2000")

    automaticModule("batik-parser-1.14.jar", "batik.parser")

    automaticModule("google-diff-match-patch-0.1.jar", "google.diffmatchpatch")
    automaticModule("unique4j-1.4.jar", "uniqueforj")

}

dependencies {

    implementation 'org.jetbrains:annotations:22.0.0'

    // JAVA FX //
    implementation(group: 'org.jfxtras', name: 'jmetro', version: '11.6.15') {
        exclude group: 'org.openjfx'
    }
    // ControlsFX
    implementation group: 'org.controlsfx', name: 'controlsfx', version: '11.1.1'

    // Support special images format when render a PDF
    implementation group: 'com.github.jai-imageio', name: 'jai-imageio-core', version: '1.4.0'
    implementation group: 'com.github.jai-imageio', name: 'jai-imageio-jpeg2000', version: '1.4.0'
    implementation group: 'org.apache.pdfbox', name: 'jbig2-imageio', version: '3.0.3'

    // Signing and encrypting pdf (not used)
    //compile group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.53'

    // Image IO - EXIF
    implementation group: 'com.drewnoakes', name: 'metadata-extractor', version: '2.16.0'

    // JSON requests
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.13.1'
    // YAML
    implementation group: 'org.yaml', name: 'snakeyaml', version: '1.29'
    // LaTeX
    implementation 'org.scilab.forge:jlatexmath:1.0.7'
    implementation(name: 'writer2latex', ext: 'jar')

    // OSX
    implementation 'de.jangassen:nsmenufx:3.1.0'

    // SVG
    implementation group: 'org.apache.xmlgraphics', name: 'batik-parser', version: '1.14'
    implementation group: 'de.rototor.pdfbox', name: 'graphics2d', version: '0.34'

    // PDF Box
    implementation group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.0.25'

    // Text Google diff / match / patch
    implementation group: 'google-diff-match-patch', name: 'google-diff-match-patch', version: '0.1'

    // Unique4J
    implementation 'tk.pratanumandal:unique4j:1.4'


}

javafx {
    version = '16'
    modules = ['javafx.controls', 'javafx.swing', 'javafx.fxml']
}

def os = org.gradle.internal.os.OperatingSystem.current()
// On 32bits, JDK must include JavaFX (the default JavaFX is 64bits)
// set JAVA_HOME=C:\Program Files\AdoptOpenJDK\jdk-17.0.2-bell-full-win32
// $Env:JAVA_HOME = "C:\Program Files\AdoptOpenJDK\jdk-17.0.2-bell-full-win32"
def win32 = false

version = "1.3.1"
description = "PDF students assessments editor"

//frenchDescription="Éditeur de copies PDF"

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"
mainClassName = "fr.clementgre.pdf4teachers/fr.clementgre.pdf4teachers.Main"

application {
    mainModule.set('fr.clementgre.pdf4teachers')
    mainClass.set('fr.clementgre.pdf4teachers.Main')
}

// Xlint check
/*gradle.projectsEvaluated {
    tasks.withType(JavaCompile){
        options.compilerArgs << "-Xlint:unchecked"
    }
}*/

run {
    jvmArgs = [
            '--add-exports=javafx.graphics/com.sun.javafx.scene=org.controlsfx.controls',
            '--add-exports=javafx.graphics/com.sun.javafx.scene.traversal=org.controlsfx.controls'
    ]
    if (os.macOsX) {
        // The dock icon works only when app is in /Applications
        jvmArgs += '-Xdock:icon=/Applications/PDF4Teachers.app/Contents/Resources/logo.icns'
    }
}

jlink {
    options = [
            '--strip-debug',
            '--compress', '2',
            '--no-header-files',
            '--no-man-pages'
    ] as List<String>
    launcher {
        name = 'PDF4Teachers'
        jvmArgs = [
                '--add-exports=javafx.graphics/com.sun.javafx.scene=org.controlsfx.controls',
                '--add-exports=javafx.graphics/com.sun.javafx.scene.traversal=org.controlsfx.controls'
        ]
        if (os.macOsX) {
            // The dock icon works only when app is in /Applications
            jvmArgs += '-Xdock:icon=/Applications/PDF4Teachers.app/Contents/Resources/logo.icns'
        }
    }

    String jdkPath = project.findProperty('jdkPath')
    String platform = os.windows ? (win32 ? 'win32' : 'win') : (os.macOsX ? 'mac' : 'lin')

    if(jdkPath != null){ // Use ./gradlew jpackage -PjdkPath=xxx
        System.out.println("Using JDK passed in property: ".concat(jdkPath))
        javaHome.set(jdkPath)

    }else{
        if(os.windows){
            if(win32) javaHome.set('C:\\Program Files\\AdoptOpenJDK\\jdk-17.0.2-bell-full-win32')// C:\Program Files\BellSoft\LibericaJDK-17-Full
            else javaHome.set('C:\\Program Files\\AdoptOpenJDK\\jdk-17.0.1.12-hotspot')
        }
        else if(os.macOsX) javaHome.set('/Library/Java/JavaVirtualMachines/temurin-17.jdk/Contents/Home')
        else if(os.linux) javaHome.set('/opt/jdk-17')
        System.out.println("Using JDK: ".concat(javaHome.get()))
    }

    targetPlatform(platform, javaHome.get())

    jpackage {
        targetPlatformName = platform

        installerType = os.windows ? 'msi' : (os.macOsX ? 'dmg' : 'deb')
        installerOptions = [
                '--description', project.description,
                '--copyright', 'Copyrigth 2019-2022 Clément Grennerat',
                '--app-version', project.version,
                '--vendor', "Clément Grennerat",
                '--type', installerType
        ] as List<String>

        if (os.windows) { // WINDOWS
            imageOptions += ['--icon', 'src/main/resources/logo.ico']
            installerOptions += [
                    '--win-dir-chooser',
                    '--win-menu',
                    '--win-shortcut',
                    '--win-menu-group', '',
                    '--file-associations', 'distribution/windows/association-win.properties'
            ]
        } else if (os.macOsX) { // MAC
            imageOptions += ['--icon', 'src/main/resources/logo.icns']
            installerOptions += [
                    "--mac-package-name", "PDF4Teachers"
            ]
        } else if (os.linux) { // LINUX
            imageOptions += ['--icon', 'src/main/resources/logo.png']
            installerOptions += [
                    '--linux-menu-group', 'Education;Office',
                    '--linux-shortcut',
                    '--file-associations', 'distribution/linux/association-lin.properties'
            ]
            if (installerType == 'deb') installerOptions += ['--linux-deb-maintainer', 'clement.grennerat@free.fr']
            else if (installerType == 'rpm') installerOptions += ['--linux-rpm-license-type', 'Apache-2.0']
        }
    }

    mergedModule{

        // JBig2
        uses "org.apache.pdfbox.jbig2.util.log.LoggerBridge"
        uses "org.apache.pdfbox.jbig2.util.cache.CacheBridge"

        // Generated automatically by gradle
        requires 'java.logging'
        requires 'jdk.xml.dom'
        requires 'java.sql'
        requires 'java.xml'
        requires 'java.desktop' // needed for jbig2 LoggerBridge
        uses 'javax.imageio.spi.ImageWriterSpi'
        provides 'org.apache.xmlgraphics.image.loader.spi.ImageConverter' with 'org.apache.xmlgraphics.image.loader.impl.ImageConverterBitmap2G2D',
                'org.apache.xmlgraphics.image.loader.impl.ImageConverterBuffered2Rendered',
                'org.apache.xmlgraphics.image.loader.impl.ImageConverterG2D2Bitmap',
                'org.apache.xmlgraphics.image.loader.impl.ImageConverterRendered2PNG'
        provides 'javax.imageio.spi.ImageOutputStreamSpi' with 'com.github.jaiimageio.impl.stream.ChannelImageOutputStreamSpi'
        provides 'org.apache.xmlgraphics.image.writer.ImageWriter' with 'org.apache.xmlgraphics.image.writer.imageio.ImageIOJPEGImageWriter',
                'org.apache.xmlgraphics.image.writer.imageio.ImageIOPNGImageWriter',
                'org.apache.xmlgraphics.image.writer.imageio.ImageIOTIFFImageWriter',
                'org.apache.xmlgraphics.image.writer.internal.PNGImageWriter',
                'org.apache.xmlgraphics.image.writer.internal.TIFFImageWriter'
        provides 'javax.imageio.spi.ImageReaderSpi' with 'com.github.jaiimageio.impl.plugins.bmp.BMPImageReaderSpi',
                'com.github.jaiimageio.impl.plugins.pcx.PCXImageReaderSpi',
                'com.github.jaiimageio.impl.plugins.pnm.PNMImageReaderSpi',
                'com.github.jaiimageio.impl.plugins.raw.RawImageReaderSpi',
                'com.github.jaiimageio.impl.plugins.tiff.TIFFImageReaderSpi',
                'com.github.jaiimageio.impl.plugins.wbmp.WBMPImageReaderSpi',
                'com.github.jaiimageio.jpeg2000.impl.J2KImageReaderSpi',
                'org.apache.pdfbox.jbig2.JBIG2ImageReaderSpi'
        provides 'org.apache.xmlgraphics.image.loader.spi.ImageLoaderFactory' with 'org.apache.xmlgraphics.image.loader.impl.ImageLoaderFactoryEPS',
                'org.apache.xmlgraphics.image.loader.impl.ImageLoaderFactoryInternalTIFF',
                'org.apache.xmlgraphics.image.loader.impl.ImageLoaderFactoryPNG',
                'org.apache.xmlgraphics.image.loader.impl.ImageLoaderFactoryRaw',
                'org.apache.xmlgraphics.image.loader.impl.ImageLoaderFactoryRawCCITTFax',
                'org.apache.xmlgraphics.image.loader.impl.imageio.ImageLoaderFactoryImageIO'
        provides 'org.apache.xmlgraphics.image.loader.spi.ImagePreloader' with 'org.apache.xmlgraphics.image.loader.impl.PreloaderBMP',
                'org.apache.xmlgraphics.image.loader.impl.PreloaderEMF',
                'org.apache.xmlgraphics.image.loader.impl.PreloaderEPS',
                'org.apache.xmlgraphics.image.loader.impl.PreloaderGIF',
                'org.apache.xmlgraphics.image.loader.impl.PreloaderJPEG',
                'org.apache.xmlgraphics.image.loader.impl.PreloaderRawPNG',
                'org.apache.xmlgraphics.image.loader.impl.PreloaderTIFF',
                'org.apache.xmlgraphics.image.loader.impl.imageio.PreloaderImageIO'
        provides 'javax.imageio.spi.ImageInputStreamSpi' with 'com.github.jaiimageio.impl.stream.ChannelImageInputStreamSpi'
        provides 'javax.xml.transform.URIResolver' with 'org.apache.xmlgraphics.util.uri.DataURIResolver'
        provides 'org.apache.pdfbox.jbig2.util.log.LoggerBridge' with 'org.apache.pdfbox.jbig2.util.log.JDKLoggerBridge'
        provides 'javax.imageio.spi.ImageWriterSpi' with 'com.github.jaiimageio.impl.plugins.bmp.BMPImageWriterSpi',
                'com.github.jaiimageio.impl.plugins.gif.GIFImageWriterSpi',
                'com.github.jaiimageio.impl.plugins.pcx.PCXImageWriterSpi',
                'com.github.jaiimageio.impl.plugins.pnm.PNMImageWriterSpi',
                'com.github.jaiimageio.impl.plugins.raw.RawImageWriterSpi',
                'com.github.jaiimageio.impl.plugins.tiff.TIFFImageWriterSpi',
                'com.github.jaiimageio.impl.plugins.wbmp.WBMPImageWriterSpi',
                'com.github.jaiimageio.jpeg2000.impl.J2KImageWriterSpi'
        provides 'org.apache.pdfbox.jbig2.util.cache.CacheBridge' with 'org.apache.pdfbox.jbig2.util.cache.SoftReferenceCacheBridge'
    }
}


