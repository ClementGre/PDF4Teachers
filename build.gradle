plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id 'org.beryx.jlink' version '2.17.9'
}
repositories{
    mavenCentral()
    jcenter()
}
dependencies{
    compile group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.0.17'
    compile (group: 'org.jfxtras', name: 'jmetro', version: '11.5.10'){
        exclude group: 'org.openjfx'
    }
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.10.3'
}
javafx {
    version = '14'
    modules = ['javafx.controls', 'javafx.swing', 'javafx.fxml']
}

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"
mainClassName = "fr.themsou.main.Main"

version = "1.1.0"
description = "Application pour éditer des copies PDF en grande quantité, destiné principalement aux professeurs."

task packageMsi(type: Exec){
    workingDir project.projectDir
    commandLine = ["gradlew.bat", "jpackage", "-PinstallerType=msi"]
}
task packageDmg(type: Exec){
    workingDir project.projectDir
    commandLine = ["./gradlew", "jpackage", "-PinstallerType=dmg"]
}
task packageDeb(type: Exec){
    workingDir project.projectDir
    commandLine = ["./gradlew", "jpackage", "-PinstallerType=deb"]
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'PDF4Teachers'
    }
    targetPlatform('win', 'C:\\Program Files\\AdoptOpenJDK\\jdk-14.0.1.7-hotspot')
    //targetPlatform('lin', '/home/clement/bin/AdoptOpenJDK-13/')
    //targetPlatform('mac', '/Library/Java/JavaVirtualMachines/adoptopenjdk-13.jdk/Contents/Home/')

    jpackage {
        installerOptions = [
                '--description', project.description,
                '--copyright', 'Copyrigth 2019 Clément G.',
                '--app-version', project.version,
                '--vendor', "Clément G."
        ] as List<String>

        installerType = project.findProperty('installerType') // we will pass this from the command line (example: -PinstallerType=msi)

        if(installerType == 'msi'){
            imageOptions += ['--icon', 'src/main/resources/logo.ico']
            installerOptions += [
                    '--win-dir-chooser',
                    '--win-menu',
                    '--win-shortcut',
                    '--win-menu-group', ''
            ]
        }else if(installerType == 'pkg'){
            imageOptions += ['--icon', 'src/main/resources/logo.icns']
            installerOptions += [
                    "--mac-package-name", "PDF4Teachers"
            ]

        }else if(installerType in ['deb', 'rpm']){
            imageOptions += ['--icon', 'src/main/resources/logo.png']
            installerOptions += [
                    '--linux-menu-group', 'Office',
                    '--linux-shortcut'
            ]
        }else if (installerType == 'deb'){
            installerOptions += [
                    '--linux-deb-maintainer', 'themsou.ldv@gmail.com'
            ]
        }else if (installerType == 'rpm'){
            installerOptions += [
                    '--linux-rpm-license-type', 'Apache-2.0'
            ]
        }
    }
}


